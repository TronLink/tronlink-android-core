apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'
group = 'com.github.TronLink'

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 3
        versionName "1.0.1"
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = false
        additionalParameters += '--multi-dex'
        additionalParameters += '--set-max-idx-number=35000'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}




protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
//         artifact = 'com.google.protobuf:protoc:3.5.1-1:osx-x86_64'
    }
    plugins {
//        javalite {
//            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
//        }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0'
//            artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0:osx-x86_64'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { outputSubDir = "gen" }
            }
        }
        all()*.plugins {
            grpc {
                outputSubDir = "gen"
            }
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    implementation 'com.google.code.gson:gson:2.2.4'

    //grpc
    implementation ('io.grpc:grpc-protobuf:1.14.0') {
        exclude group:'com.google.api.grpc',module:'proto-google-common-protos'
    }
    implementation 'io.grpc:grpc-okhttp:1.38.0'
    implementation 'io.grpc:grpc-stub:1.14.0'
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation 'com.google.protobuf:protobuf-java:3.4.0'

    //common
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'androidx.multidex:multidex:2.0.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.9'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.69'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    // SLF4J
    implementation 'org.slf4j:slf4j-api:1.7.25'

    implementation 'com.alibaba:fastjson:1.2.78'
    //keystore
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.8.5'

    //retrofit+rxjava
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

}
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'org.tron'
                artifactId = 'tronlink-core'
                version = "${android.defaultConfig.versionName}"
            }
        }
    }
}
